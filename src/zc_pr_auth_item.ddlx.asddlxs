@Metadata.layer: #CORE

@UI: { headerInfo: { typeName:       'Price Authorization',
                     typeNamePlural: 'Price Authorization',
                     title:          { type: #STANDARD,
                                       label: 'Material',
                                       value: 'Material' } } }

annotate entity ZC_PR_AUTH_ITEM with
{

  @UI.facet: [
        {
          purpose: #STANDARD,
          id: 'Material',
          label: 'Material',
          position   : 10,
          type: #COLLECTION
        },
        {
          parentId   : 'Material',
          label      : 'Price Protection',
          type       : #FIELDGROUP_REFERENCE,
          targetQualifier: 'PriceProtection'
        },
        {
          parentId   : 'Material',
          label      : 'Onhand & Inc',
          type       : #FIELDGROUP_REFERENCE,
          targetQualifier: 'OHI'
        },
        {
          parentId   : 'Material',
          label      : 'GM',
          type       : #FIELDGROUP_REFERENCE,
          targetQualifier: 'GM'
        },  
        {
            id         : 'Price',
            label      : 'Price',
            position   : 20,
            type       :  #LINEITEM_REFERENCE,
            targetElement: '_Price'
        },
        {
            id         : 'Customer',
            label      : 'Customer',
            position   : 30,
            type       :  #LINEITEM_REFERENCE,
            targetElement: '_Customer'
        }]

  @UI.lineItem: [{ position: 10 }]
  Material;

  @UI.lineItem: [{ position: 20 }]
  Reject;

  @UI.lineItem: [
      {
        type:#FOR_ACTION,
        label: 'Add Notes',
        dataAction: 'addNotes',
        inline: true,
        iconUrl: 'sap-icon://notes',
        emphasized: true,
        importance: #HIGH,
        position: 30
      }, { position: 30 }]
  EstoreFreeze;

  @UI.lineItem: [{ position: 40 }]
  @UI.fieldGroup: [ {
    position: 10, qualifier: 'PriceProtection', label: 'Quantity'
  }]
  @EndUserText.quickInfo : 'Price Protection Quantity'
  PriceProt;

  @UI.lineItem: [{ position: 50 }]
  @UI.fieldGroup: [ {
    position: 20, qualifier: 'PriceProtection', label: 'Impact'
  }]
  @EndUserText.quickInfo : 'Price Protection Impact at ZPX'
  PpImpact;

  @UI.lineItem: [{ position: 60 }]
  @UI.fieldGroup: [ {
    position: 10, qualifier: 'OHI', label: 'Quantity'
  }]
  OnhandInc;

  @UI.lineItem: [{ position: 70 }]
  @UI.fieldGroup: [ {
    position: 20, qualifier: 'OHI', label: 'Impact'
  }]
  OnhandIncImpact;

  @UI.lineItem: [{ position: 80 }]
  @UI.fieldGroup: [ {
    position: 10, qualifier: 'GM', label: 'Budget'
  }]
  GmBudget;

  @UI.lineItem: [{ position: 90 }]
  @UI.fieldGroup: [ {
    position: 20, qualifier: 'GM', label: 'Actual'
  }]
  GmActual;

  @UI.lineItem: [{ position: 100 }]
  @UI.selectionField: [{ position: 100 }]
  Map;
}